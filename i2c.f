RPI4 804000 + CONSTANT BSC1 

BSC1 00 + CONSTANT I2C_CONTROL
BSC1 04 + CONSTANT I2C_STATUS
BSC1 08 + CONSTANT I2C_DATA_LENGTH
BSC1 0C + CONSTANT I2C_SLAVE_ADDRESS
BSC1 10 + CONSTANT I2C_DATA_FIFO

BSC1 14 + CONSTANT I2C_CLOCK_DIVIDER
BSC1 18 + CONSTANT I2C_DATA_DELAY
BSC1 1C + CONSTANT I2C_CLOCK_STRETCH_TIMEOUT

: SET TUCK @ OR SWAP ! ;
: CLEAR SWAP INVERT OVER @ AND SWAP ! ;

GPIO2 ALT0
GPIO3 ALT0

( I2C_CONTROL )
0 1BIT_SET CONSTANT WRITE ( 0 = Write Packet Transfer )
4 1BIT_SET CONSTANT FIFO_CLEAR ( x1 = Clear FIFO. One-shot operation )
7 1BIT_SET CONSTANT START ( 1 = Start a new transfer. One-shot operation )
F 1BIT_SET CONSTANT I2CEN ( 1 = BSC controller is enabled I2C Enable)

( I2C_STATUS - Cleared by writing 1 to the field )
1 1BIT_SET CONSTANT TRANSFER_DONE ( 1 = Transfer complete. Transfer Done) 
8 1BIT_SET CONSTANT ACK_ERR ( ACK Error 0 = No errors detected. 1 = Slave has not acknowledged its address )
9 1BIT_SET CONSTANT CLKT ( Clock Stretch Timeout 0 = No errors detected. 1 = Slave has held the SCL signal low - clock stretching - for longer and that specified in the I2CCLKT register )


: RESET_FIFO FIFO_CLEAR I2C_CONTROL SET ;
: RESET_STATUS 
	TRANSFER_DONE   I2C_STATUS SET
	ACK_ERR         I2C_STATUS SET
	CLKT            I2C_STATUS SET ;

: SET_SLAVE 27 I2C_SLAVE_ADDRESS ! ;

: I2C_SETUP
	RESET_STATUS
	RESET_FIFO
	1 I2C_DATA_LENGTH !
	SET_SLAVE
;
: I2C_STORE I2C_DATA_FIFO ! ;

: I2C_SEND
	WRITE I2C_CONTROL CLEAR
	START I2C_CONTROL SET
	I2CEN I2C_CONTROL SET
;

: I2C_DELAY 1000 DELAY ;

: >I2C
	I2C_SETUP
	I2C_STORE
	I2C_SEND
	I2C_DELAY
;

: SEND SWAP >I2C >I2C ;
